/* This data is the Example 6.2 Fatigue Life Alloy T7987
 * This data is right censored data with 67 specimens failing before 
 * 30000 cycles and 5 lasting the entire test without failing
 * This demonstrates how to use different procedures to estimate
*/

data alloy;
input lifetime censor@@;
label lifetime = 'Thousands of Cycles';
datalines;
94 0 118 0 139 0 159 0 171 0 189 0 227 0 96 0 121 0 140 0 159 0 172 0 190 0 256 0
99 0 121 0 141 0 159 0 173 0 196 0 257 0 99 0 123 0 141 0 159 0 176 0 197 0 209 0
104 0 129 0 143 0 162 0 177 0 203 0 271 0 108 0 131 0 144 0 168 0 180 0 205 0 274 0
112 0 133 0 149 0 168 0 180 0 211 0 291 0 114 0 135 0 149 0 169 0 184 0 213 0
117 0 136 0 152 0 170 0 187 0 224 0 117 0 139 0 153 0 170 0 188 0 226 0 300 1 300 1
300 1 300 1 300 1
;
run;

/* Fit example using proc lifetest.  By default the method fits using Kaplan meier
 * Cl gives pointwise confidence limits; cb=ep gives the simulataneous equal precision 
 * confidence bands.  ls gives the cumulativ ehazard function
 *
*/
ods graphics on;
proc lifetest data=alloy plots=(survival(nocensor cl cb=ep) ls h /*pdf*/) outsurv=alloyout;
time lifetime*censor(1);
run;
ods graphics off;

/* We can also fit probability plots here.  First up is weibull.
 * distribution statement provides the weibull.  can change to a variety
 * probplot shows the probability plot on the probability scale, but for weibull
 * it is on the log time scale so need to change to just time scale
 * the vref line provides the values for eta, which is when log(1-p) = 0
*/

ods graphics on;
proc reliability data=alloy;
distribution weibull;
probplot lifetime*censor(1)/vref=63.2 laxis=0 to 300 by 10;
inset n nfail weibull(mean) weibull(scale) weibull(shape);
run;
ods graphics off;
